# SPN Parser Grammar #

## Productions ##

spn 		:= node inputs EOF
inputs		:= '#' ID (';' ID)*
node		:= sumNode | productNode | histogramNode | poissonNode
sumNode		:= ID 'SumNode' '(' REAL '*' ID (',' REAL '*' ID)* ')' '{' node* '}'
productNode	:= ID 'ProductNode' '(' ID (',' ID)* ')' '{' node* '}'
histogramNode	:= ID 'Histogram' '(' ID '|' '[' INT '.' (',' INT '.')* ']' ';' '[' REAL (',' REAL)* ']' ')'
poissonNode	:= ID 'P' '(' ID '|' 'lambda' '=' REAL ')'

## Terminals ##

ID	- Arbitrary string, starting with a letter
REAL	- Real number, potentially in scientific notation
INT	- Integer number, must not start with '0'

